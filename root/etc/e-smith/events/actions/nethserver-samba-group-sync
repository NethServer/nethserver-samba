#!/usr/bin/perl

#
# NethServer Samba
#
# Create/update group(s) in SAM database.
#
# The new group name is specified by the second argument to the script.
# If the group name value has not been set ALL groups in AccountDB are processed.
#
#
# Copyright (C) 2012 Nethesis S.r.l.
# http://www.nethesis.it - support@nethesis.it
# 
# This script is part of NethServer.
# 
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
# 
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use esmith::AccountsDB;
use User::grent;

my $event = shift;
my $group = shift;
my @groups;
my $accountsDb = esmith::AccountsDB->open_ro();

my $exitCode = 0;

my $net = '/usr/bin/net';

if($group) {
    if($accountsDb->get_prop($group, 'type') ne 'group') {
	die("[ERROR] Invalid group account name: `$group`");
    }
    @groups = ($group);
} else {
    # 'locals' group is created by nethserver-directory and must be in
    # SAM DB:
    @groups = ('locals', map { $_->key } $accountsDb->groups());   
}

# Check if idmap LDAP backend is configured before to proceed.  This
# action can be invoked before samba is configured (in other words:
# before nethserver-samba-update has been executed)!  For instance
# group-create and nethserver-ibays-update events use this action.
my $secretsFile = '/var/lib/samba/private/secrets.tdb';
if( ! -r $secretsFile) {
    exit 0;
}
my $secretsOutput = qx(/usr/bin/tdbtool $secretsFile keys);
if( $secretsOutput !~ m(SECRETS/GENERIC/IDMAP_LDAP_\*)m ) {
    warn "[WARNING] Idmap backend is still not configured. Action skipped\n";
    exit 0;
}


foreach my $group (@groups) {


    # Check if a SAM entry exists (group or user)
    my $entrySid = qx($net sam show "$group" 2>/dev/null);
    chomp($entrySid);
    if($? != 0) {
	#
	# Create the SAM group entry
	#

	my $groupEntry = getgrnam($group);
	if( ! $groupEntry) {
	    warn "[WARNING] Group entry '$group' not found! Skipped.\n";
	    next;
	}

	# Map the unix group as samba domain group:
	system($net, 'sam', 'mapunixgroup', $group);
	if($? != 0) {
	    warn "[ERROR] Failed to add group `$group` in SAM database!\n";
	    $exitCode ++;
	    next;
	}
    }

    #
    # Update the SAM group entry
    #
    my $description = $accountsDb->get_prop($group, 'Description') || '';
    system($net, qw(sam set comment), $group, $description);
    if($? != 0) {
	warn "[ERROR] Failed to set group `$group` comment attribute in SAM database!\n";
	$exitCode ++;
	next;
    }

}

exit(($exitCode > 0) ? 1 : 0);


